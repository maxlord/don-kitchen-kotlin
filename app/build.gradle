import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

android {
	compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
	buildToolsVersion '23.0.3'

	defaultConfig {
		minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
		targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)

		applicationId "ru.ls.donkitchen"
		versionCode 130
		versionName "1.3"

		buildConfigField "int", "DATABASE_VERSION", "1";
		buildConfigField "String", "API_URL", "\"http://dk.lord-services.ru/api/v1/\"";
		buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
	}

	signingConfigs {
		debug {

		}

		release {
			storeFile file(System.getenv("LS_APP_PRIVATE_KEY"))
			keyAlias System.getenv("LS_APP_ALIAS")
			storePassword System.getenv("LS_APP_STORE_PW")
			keyPassword System.getenv("LS_APP_PW")
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	packagingOptions {
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/services/javax.annotation.processing.Processor'
	}

	lintOptions {
		abortOnError false
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}

	configurations.all {
		resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	testCompile 'junit:junit:4.12'

	compile supportDependencies.appCompat
	compile supportDependencies.supportV4
	compile supportDependencies.supportV13
	compile supportDependencies.recyclerView
	compile supportDependencies.cardView
	compile supportDependencies.design

	compile googlePlayServices.base
//	compile googlePlayServices.location
//	compile googlePlayServices.maps
	compile googlePlayServices.fcm

	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile 'com.google.dagger:dagger:2.7'
	kapt 'com.google.dagger:dagger-compiler:2.7'
//	provided 'org.glassfish:javax.annotation:10.0-b28'
//	 javax.annotation:javax.annotation-api:1.2
	compile 'com.jakewharton.timber:timber:4.1.2'

	// Rx
	compile 'io.reactivex:rxkotlin:0.40.1'
	compile 'io.reactivex:rxandroid:1.2.0'
	compile 'com.trello:rxlifecycle:0.8.0'
	compile 'com.trello:rxlifecycle-components:0.8.0'
	compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.4.0'
	compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:0.4.0'

	// Retrofit
	compile 'com.squareup.retrofit2:retrofit:2.1.0'
	compile 'com.squareup.retrofit2:converter-gson:2.1.0'
	compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
	// OkHttp
	compile 'com.squareup.okhttp3:okhttp:3.4.1'
	compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

	// Anko
	compile 'org.jetbrains.anko:anko-sdk15:0.9' // sdk19, sdk21, sdk23 are also available
	compile 'org.jetbrains.anko:anko-appcompat-v7:0.9' // For appcompat-v7 bindings

	compile 'com.squareup:otto:1.3.8'

	compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
		transitive = true;
	}

	compile 'com.github.rey5137:material:1.2.2'

	// OrmLite
	compile 'com.j256.ormlite:ormlite-core:5.0'
	compile 'com.j256.ormlite:ormlite-android:5.0'
	compile 'org.flywaydb:flyway-core:4.0'
	compile 'org.sqldroid:sqldroid:1.0.3'

	// Glide
	compile 'com.github.bumptech.glide:glide:3.7.0'
}

kapt {
	generateStubs = true
}

apply from: '../publishRelease.gradle'


def buildTime() {
	def df = new SimpleDateFormat("dd.MM.yyyy")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))

	return df.format(new Date())
}

repositories {
	mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
